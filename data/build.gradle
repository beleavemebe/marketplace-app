plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

String PROPERTY_API_KEY = "API_KEY"

def apiKeyProps = new Properties()
try {
    apiKeyProps.load(new FileInputStream(rootProject.file("apikey.properties")))
} catch (Exception e) {
    def ciKey = System.getenv(PROPERTY_API_KEY)
    if (ciKey != null) {
        apiKeyProps.setProperty(PROPERTY_API_KEY, ciKey)
    }
}

android {
    compileSdk 32

    defaultConfig {
        minSdk 24
        targetSdk 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        def apiKey = apiKeyProps[PROPERTY_API_KEY]
        if (apiKey != null) {
            buildConfigField("String", "API_KEY", apiKey)
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation "io.insert-koin:koin-core:3.1.5"
    testImplementation "io.insert-koin:koin-test:3.1.5"

    implementation project(path: ':domain')
    implementation project(path: ':apiclient')

    def coroutines_version = "1.6.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${coroutines_version}"

    testImplementation 'junit:junit:4.13.2'
}
